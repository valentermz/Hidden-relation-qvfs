----------------------------------------------------------------------
-- MACAULAY2 CODE TO COMPUTE THE HIDDEN RELATION VIA THE MODULI MAP --
----------------------------------------------------------------------

-- Hidden relation in terms of t_0..t_2,d_0..d_2, and L=\Lambda=\prod \lambda_k 
-- We first work with the variable X that represents X=-(d_0*d_1+d_1*d_2+d_2*d_0)/L
-- After obtaining a relation in terms of X we convert we substitute L in
-- The final polynomial is H = H0+H1*L+H2*L^2

printWidth=120; gbTrace=3;

-- We start with the following rings:
kk = QQ
R = kk[a_0..a_5]
S = kk[t_0..t_2,d_0..d_2,X,L,Degrees=>{1,1,1,2,2,2,4,0}]

-- Formulas for the traces
T_0 = -a_0-a_5
T_1 = a_0+a_4-a_5
T_2 = -a_0+a_1+a_5

-- Formulas for the determinants
D_0 = -a_2*a_3+a_0*a_5
D_1 = -a_1*a_3+a_2*a_3+a_0*a_4-a_0*a_5
D_2 = a_2*a_3-a_2*a_4-a_0*a_5+a_1*a_5

-- Explicit formula for X
x = 4*a_0^3*a_2-a_0^2*a_1^2+2*a_0^2*a_1*a_5-12*a_0^2*a_2*a_4-a_0^2*a_5^2+2*a_0*a_1^2*a_4+18*a_0*a_1*a_2*a_3-4*a_0*a_1*a_4*a_5-18*a_0*a_2*a_3*a_5+12*a_0*a_2*a_4^2+2*a_0*a_4*a_5^2-4*a_1^3*a_3+12*a_1^2*a_3*a_5-a_1^2*a_4^2-18*a_1*a_2*a_3*a_4-12*a_1*a_3*a_5^2+2*a_1*a_4^2*a_5+27*a_2^2*a_3^2+18*a_2*a_3*a_4*a_5-4*a_2*a_4^3+4*a_3*a_5^3-a_4^2*a_5^2;

-- The list ls defines a function F:R<---S
ls = {T_0,T_1,T_2,D_0,D_1,D_2,x,0};
F = map(R,S,ls);

-- Main computations
time G = gens kernel F;

-- The kernel of F is generated by a polynomial p (and L, since everything lives in the hyperplane L=0)
G_(0,0) == L
p = G_(0,1);

-- The following are the coefficients of p as a function of X; p=AX^2+BX+C:
A = substitute(diff(X,diff(X,p/2)),{X=>0});
B = substitute(diff(X,p),{X=>0});
C = substitute(p,{X=>0});

-- The coefficients of the Hidden relation in our family are:
H0 = A*(d_0*d_1+d_1*d_2+d_2*d_0)^2;
H1 = -B*(d_0*d_1+d_1*d_2+d_2*d_0);
H2 = C;

-- The hiden relation
H = H0+H1*L+H2*L^2

-- Properties of H
isPrime H
isHomogeneous H
size H

-- Work with a sub-family (eg. Hamiltonian vector fields)
subVals = {t_0 => 0, t_1 => 0, t_2 => 0};
HVals = sub(H, subVals);
factor HVals  -- hidden relation for the Hamiltonian family

-- Irreducibility of B and C
isPrime A  --false
factor A

isPrime B  --true

isPrime C  --true

--------------------
-- Export the H_i --
--------------------

toString H0
toString H1
toString H2
